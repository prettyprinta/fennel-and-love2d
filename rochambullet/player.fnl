(local Character (require "rochambullet.character"))
(local Player (Character:extend))
(tset Player :new (fn [self x y]
  (self.super.new self x y 256 0)
  (set self.i (love.graphics.newImage "bin/howtolove/arrow_right.png"))
  (set self.ox (/ (self.i:getWidth) 2))
  (set self.oy (/ (self.i:getHeight) 2))
  (set self.scale 0.5)
  (set self.keys {})
  (set self.dir [])
  (set self.mx 0)
  (set self.my 0)
  (set self.attack 0)
  (set self.duration (/ 1 8))
  self))
(tset Player :moving (fn [self]
  (set self.dir [])
  (when (and self.keys.down (not self.keys.up)) (table.insert self.dir :s))
  (when (and self.keys.up (not self.keys.down)) (table.insert self.dir :n))
  (when (and self.keys.left (not self.keys.right)) (table.insert self.dir :w))
  (when (and self.keys.right (not self.keys.left)) (table.insert self.dir :e))
  (match self.dir
    [:s :w]  (set self.angle (* math.pi 0.75))
    [:n :w]  (set self.angle (* math.pi 1.25))
    [:s :e]  (set self.angle (* math.pi 0.25))
    [:n :e]  (set self.angle (* math.pi 1.75))
    [:w]     (set self.angle (* math.pi 1.00))
    [:e]     (set self.angle (* math.pi 0.00))
    [:s]     (set self.angle (* math.pi 0.50))
    [:n]     (set self.angle (* math.pi 1.50))
    )))
(tset Player :draw (fn [pc w h]
  (love.graphics.setColor 1 1 1 1)
  (love.graphics.draw pc.i pc.x pc.y pc.angle pc.scale pc.scale pc.ox pc.oy)
  (love.graphics.setColor 1 0.25 0.5 1)
  (let [stdarc  (* (math.max pc.ox pc.oy) pc.scale)
        attack  (* (math.sin (/ (* pc.attack math.pi) pc.duration)) 25) 
        aim     (- (math.atan2 (- pc.mx pc.x) (- pc.y pc.my)) (/ math.pi 2))
        arca    (- aim (/ math.pi 4))
        arcb    (+ aim (/ math.pi 4))]
    (if (> pc.attack 0)
      (love.graphics.arc "fill" "open" pc.x pc.y (+ stdarc attack) arca arcb)
      (love.graphics.arc "line" "open" pc.x pc.y stdarc arca arcb)))
  (love.graphics.setColor 1 1 1 1)))
Player
